plugins {
    id 'java'
    id 'io.freefair.lombok' version "${lombokPluginVersion}"
    id "io.qameta.allure" version "${allureVersion}"
    id 'org.owasp.dependencycheck' version "${owaspDependencycheckPluginVersion}"
    id 'com.github.ben-manes.versions' version "${dependencyUpdatesPluginVersion}"
    id 'be.vbgn.ci-detect' version "${ciDetectPluginVersion}"
}

defaultTasks 'clean', 'test'

group 'org.playwright.automation'
version '1.0-SNAPSHOT'
ext{
    if(!project.hasProperty('suiteFile')){
        suiteFile = 'src/test/resources/testRunners/testng.xml'
    }
}
repositories {
    mavenCentral()
}

dependencies {
    implementation "com.microsoft.playwright:playwright:${playwrightVersion}"
    implementation ("org.jetbrains:annotations:${annotationsVersion}"){
        exclude group: 'org.opentest4j', module: 'opentest4j'
    }
    implementation "org.testng:testng:${testNGVersion}"
    implementation ("org.junit.jupiter:junit-jupiter:${junitVersion}"){
        exclude group: 'org.opentest4j', module: 'opentest4j'
    }
    implementation "org.apache.commons:commons-lang3:${commonslang3Version}"
    implementation "org.apache.poi:poi:${poiVersion}"
    implementation "org.apache.poi:poi-ooxml:5.4.0"
    implementation "org.apache.poi:poi-ooxml-lite:${poOoxlSchemasVersion}"
    testRuntimeOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    implementation "org.projectlombok:lombok:${lombokVesion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVesion}"

    //these all are extra you can take it off if not required
    implementation "joda-time:joda-time:${jodaTimeVersion}"

    implementation "org.apache.xmlbeans:xmlbeans:${xmlBeansVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.aventstack:extentreports:${extentReportsVersion}"
    implementation "org.json:json:${jsonVersion}"


    testImplementation "org.assertj:assertj-joda-time:${assertjJodatimeVersion}"
    testImplementation "org.assertj:assertj-core:${assertjcoreVersion}"


}

// Helper to find Node.js executable on the system. Returns a File or null.
static def findNodeExecutable() {
    def candidates = []
    // environment overrides (keep backward compatibility with old typo)
    def envNodePath = System.getenv('PLAYWRIGHT_NODE_PATH') ?: System.getenv('PLAYWRIGHT_NOTE_PATH')
    if (envNodePath) candidates << new File(envNodePath)

    // allow override via system property
    def propNodePath = System.getProperty('playwright.nodePath')
    if (propNodePath) candidates << new File(propNodePath)

    // search PATH for node/node.exe
    def pathEnv = System.getenv('PATH') ?: ''
    pathEnv.split(File.pathSeparator).each { p ->
        if (p) {
            candidates << new File(p, 'node.exe')
            candidates << new File(p, 'node')
        }
    }

    candidates = candidates.findAll { it != null }.collect { it.absoluteFile }.toSet().toList()
    return candidates.find { it.exists() && it.isFile() }
}

// Task that verifies Node.js is available. Attach this to build/test so it runs during build.
tasks.register('verifyNode') {
    group = 'verification'
    description = 'Verify Node.js is installed and available for Playwright'
    doLast {
        def nodeFile = findNodeExecutable()
        if (!nodeFile) {
            throw new GradleException("Node.js executable not found. Please install Node.js or set environment variable PLAYWRIGHT_NODE_PATH to point to node.exe, or ensure 'node' is on your PATH.")
        }
        println "Verified Node.js at: ${nodeFile.absolutePath}"
        // store resolved path for other tasks to consume
        project.ext.PLAYWRIGHT_NODE_PATH = nodeFile.absolutePath
    }
}

// Make sure build and test run the verification before other actions
tasks.named('build') { dependsOn tasks.named('verifyNode') }
tasks.named('test')  { dependsOn tasks.named('verifyNode') }

test {//this is the gradle task to be executed
    systemProperties 'release' : System.getProperty('release')
    systemProperties 'headless' : System.getProperty('headless',"false")
    systemProperties 'browser' : System.getProperty('browser','chrome')
    systemProperties 'env' : System.getProperty('env','dev')
    environment "PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD", "1"
    environment "PLAYWRIGHT_NODE_PATH", "C:\\Program Files\\nodejs\\node.exe"

    // Ensure Node.js is available before running tests. This will fail the build early with a clear message.
    doFirst {
        // Prefer resolved value from verifyNode (project.ext) if present, otherwise try to locate again.
        def nodeFile
        if (project.hasProperty('PLAYWRIGHT_NODE_PATH')) {
            nodeFile = new File(project.PLAYWRIGHT_NODE_PATH)
        } else {
            nodeFile = findNodeExecutable()
        }

        if(!nodeFile || !nodeFile.exists() || !nodeFile.isFile()) {
            throw new GradleException("Node.js executable not found. Please install Node.js or set environment variable PLAYWRIGHT_NODE_PATH to point to node.exe, or ensure 'node' is on your PATH.")
        } else {
            println "Using Node.js at: ${nodeFile}"
            // ensure Playwright sees the node path during the test execution
            environment.put('PLAYWRIGHT_NODE_PATH', nodeFile.absolutePath)
        }
    }

    useTestNG() { //Tells Gradle to use TestNG
        def groupName = System.getProperty('groups')
        if(groupName && !groupName.isEmpty()){
            includeGroups groupName
        }
        suites suiteFile
    }
    outputs.upToDateWhen {false}
    //To display the following test events
    testLogging {
        events=["STARTED","PASSED", "SKIPPED", "FAILED"]
        showStandardStreams =  true
        exceptionFormat = 'full'
    }
}
tasks.register('runPlaywrightTrace', JavaExec) {
    group = "verification"
    description = "show the test trace"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.microsoft.playwright.CLI'
    args 'show-trace'
}
tasks.register('runPlaywrightCodegen', JavaExec) {
    group = "Execution"
    description = "run playwright codegen"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.microsoft.playwright.CLI'
    args 'codegen', '--channel=chrome'
}
tasks.register('runPlaywrightUW', JavaExec) {
    group = "verification"
    description = "run Playwright UW"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.microsoft.playwright.CLI'
    args = ['codegen','https://github.com/', '--browser=chromium --channel=msedge']
}

tasks.register('runEncrypter', JavaExec) {
    group = "utility"
    description = "Run AES encrypter utility"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.org.automation.appname.utils.AESUtil'
    standardInput = System.in
}
